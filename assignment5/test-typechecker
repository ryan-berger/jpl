#!/usr/bin/env perl

use strict;
use autodie;
use warnings;
use File::Spec;

my $exe = File::Spec->rel2abs($0);
die unless ($exe =~ /^(.*)\/test-typechecker$/);
my $dir = "${1}/typechecker-tests";
die unless -d $dir;

my @files = glob "${dir}/*.jpl";

sub runit($$) {
    (my $cmd, my $f) = @_;
    print "running command: `$cmd`\n";
    system "$cmd > ${f}.output 2>&1";
    open my $INF, "<${f}.output" or die;
    my $good = 0;
    my $bad = 0;
    while (my $line = <$INF>) {
        $good++ if ($line =~ /Compilation succeeded/);
        $bad++ if ($line =~ /Compilation failed/);
    }
    close $INF;
    # print "good = $good, bad = $bad\n";
    
    # return 2 if (($good + $bad) != 1);
    return 1 if ($bad == 1);
    return 0 if ($good == 1);
    die "whoops!";
}

sub lookat($) {
    (my $f) = @_;
    my $ok = 0;
    my $error = 0;
    open my $INF, "<$f" or die;
    while (my $line = <$INF>) {
        if ($line =~ /^\/\/ OK$/) {
            $ok = 1;
        }
        if ($line =~ /^\/\/ ERROR$/) {
            $error = 1;
        }
    }
    close $INF;
    die "bogus test case '$f'" unless ($ok + $error) == 1;
    return $error;
}

sub pr($) {
    (my $x) = @_;
    return ($x == 0) ? "OK   " : "ERROR";
}

my $fails = 0;

foreach my $f (@files) {
    print "\n";
    
    my $expect = lookat($f);

    my $got = runit("make run-a5t TEST=$f", $f);

    print "$f : expect = ".pr($expect).", got = ".pr($got);
    if ($got != $expect) {
        print "    <----------------- OOPS";
        $fails++;
    } else {
        if ($expect == 0) {
            system "racket ${dir}/pp.rkt < ${f}.output > ${f}.output.pp";
            my $res = system "diff -c ${f}.expected ${f}.output.pp";
            if ($res != 0) {
                print "    <----------------- OOPS";
                $fails++;
            }
        }
    }
    
    print "\n";
    ## last if $fails > 3;
}

print "\n";

if ($fails == 0) {
    print "All tests pass.\n";
} else {
    print "${fails} tests fail.\n";
}

